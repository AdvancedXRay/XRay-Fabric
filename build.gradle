plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

def ENV = System.getenv();
archivesBaseName = "${project.archives_base_name}"
version = "${project.minecraft_version}-${project.mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
group = project.maven_group

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }
    repositories {
        maven {
            url "https://maven.saps.dev/releases"
            credentials {
                username "mikeymods"
                password "${ENV.SAPS_TOKEN}"
            }
        }
    }
}


if (ENV.CURSE_DEPLOY_TOKEN) {
    curseforge {
        apiKey = ENV.CURSE_DEPLOY_TOKEN
        project {
            id = "444663"
            releaseType = "release"
            addGameVersion "Fabric"
            addGameVersion "$minecraft_version"
            changelog = file('CHANGELOG.md')
            changelogType = 'markdown'
            mainArtifact(remapJar)
        }
    }
}